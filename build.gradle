group 'com.hazebyte'
version '1.0.0'

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'org.hidetake.ssh'
apply plugin: "com.github.johnrengelman.shadow"

sourceCompatibility = 1.8

buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath "com.github.jengelman.gradle.plugins:shadow:2.0.2"
        classpath 'org.hidetake:gradle-ssh-plugin:2.9.0'
    }
}

repositories {
    mavenCentral()
    mavenLocal()
    jcenter()

    maven   {     url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'  }
    maven   {     url = "https://repo.aikar.co/content/groups/aikar/" }
    maven   {     url = 'https://nexus.hazebyte.com/repo/maven-snapshots/' }
    maven   {     url = 'http://repo.citizensnpcs.co/' }
}


dependencies {
    compile group: 'org.bukkit', name: 'bukkit', version: '1.12.1-R0.1-SNAPSHOT'
    compile group: 'net.citizensnpcs', name: 'citizensapi', version: '2.0.20-SNAPSHOT'
    compile "com.hazebyte:CrateReloadedAPI:1.0.0"
    compile "co.aikar:acf-core:0.5.0-SNAPSHOT"
    compile "co.aikar:acf-bukkit:0.5.0-SNAPSHOT"

    testCompile group: 'junit', name: 'junit', version: '4.12'
}

compileJava {
    options.compilerArgs += ["-parameters"]
    options.fork = true
    options.forkOptions.executable = 'javac'
}

shadowJar {
    archiveName = 'CrateReloadedNPC.jar'
    configurations = [project.configurations.compile]
    dependencies {
        include(dependency('co.aikar:acf-core:0.5.0-SNAPSHOT'))
        include(dependency('co.aikar:acf-bukkit:0.5.0-SNAPSHOT'))
        include(dependency('co.aikar:minecraft-timings:1.0.4'))
        include(dependency('co.aikar:locales:1.0-SNAPSHOT'))
        include(dependency('co.aikar:Table:1.0.0-SNAPSHOT'))
        include(dependency('net.jodah:expiringmap:0.5.8'))
    }
    relocate 'co.aikar.timings.lib', 'com.hazebyte.utils.timings'
    relocate 'co.aikar.commands', 'com.hazebyte.acf'
    exclude 'META-INF/*'
}

remotes {
    server {
        host = gradle.host
        user = gradle.user
        identity = gradle.identity
        knownHosts = addHostKey(file("${System.properties['user.home']}/.ssh/known_hosts"))
    }
}

task deploy {
    doLast {
        def file = "build/libs/${shadowJar.archiveName}"
        ssh.run {
            session(remotes.server) {
                put from: file, into: gradle.uploadDir
            }
        }
    }
}

build.dependsOn shadowJar
